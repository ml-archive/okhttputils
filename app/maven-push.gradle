apply plugin: 'signing'
apply plugin: 'maven'

// Username & password for Sonatype, stored in gradle.properties
def _ossrhUsername = this.properties['NEXUS_USERNAME']
def _ossrhPassword = this.properties['NEXUS_PASSWORD']

// Artifact settings
def _group = 'dk.nodes.utils'
def _version = '0.9'
def _archivesBaseName = 'okhttp'

def _name = 'Various utils for OkHttp'
def _description = 'Various utils for OkHttp'


afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = _group
                pom.artifactId = _archivesBaseName
                pom.version = _version

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }

                pom.project {
                    name _name
                    packaging 'aar'
                    description _description
                    url 'https://github.com/nodes-android/okhttputils'
                    inceptionYear '2016'

                    scm {
                        url 'https://github.com/nodes-android/okhttputils'
                        connection 'scm:https://github.com/nodes-android/okhttputils.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'johsoe'
                            name 'Johnny SÃ¸rensen'
                            email 'joso@nodes.dk'
                        }
                    }

                    issueManagement {
                        system 'GitHub issues'
                        url 'https://github.com/nodes-android/okhttputils/issues'
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
    }

}